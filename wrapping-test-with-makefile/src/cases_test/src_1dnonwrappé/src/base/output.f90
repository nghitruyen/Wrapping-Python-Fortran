!======================================================================================================================!
!
!                    DassFlow Version 2.0
!
!======================================================================================================================!
!
!  Copyright University of Toulouse-INSA - CNRS (France)
!
!  This file is part of the DassFlow software (Data Assimilation for Free Surface Flows).
!  DassFlow is a computational software aiming at simulating geophysical free surface flows.
!  It is designed for Variational Data Assimilation (4D-var) and sensitivity analyses. 
! Inverse capabilities are based on the adjoint code which is generated by 
! a source-to-source algorithmic differentiation (Tapenade software used).
!
!  DassFlow software includes few mostly independent "modules" with common architectures and structures:
!    - DassFlow 2DShallow (shallow water equations in (h,q), finite volumes), i.e. the present code.
!    - DassFlow3D  (non-linear Stokes equations, finite elements, mobile geometries, ALE).
!    - DassFlow 1D (shallow water equations in (S,Q), finite volumes or finite differences), river hydraulics.
!  Please consult the DassFlow webpage for more details: http://www.math.univ-toulouse.fr/DassFlow
!
! You have used DassFlow in an article, a scientific document etc ?  How to cite us ? Please consult the webpage.
! 
!  Many people have contributed to the DassFlow developments from the initial version to the latest ones.
!  Current main developers or scientific contributers are:
!               P. Brisset (CNES & Mathematics Institute of Toulouse & INSA Toulouse)
!               F. Couderc (CNRS & Mathematics Institute of Toulouse IMT)
!               P.-A. Garambois (INSA & ICUBE Strasbourg)
!               J. Monnier (INSA & Mathematics Institute of Toulouse IMT).
!               J.-P. Vila (INSA & Mathematics Institute of Toulouse IMT).
!  and former other developers (R. Madec, M. Honnorat and J. Marin).
!
!  Scientific contact : jerome.monnier@insa-toulouse.fr
!  Technical  contacts : frederic.couderc@math.univ-toulouse.fr, pierre.brisset@insa-toulouse.fr
!
!  This software is governed by the CeCILL license under French law and abiding by the rules of distribution
!  of free software. You can use, modify and/or redistribute the software under the terms of the CeCILL license
!  as circulated by CEA, CNRS and INRIA at the following URL: "http://www.cecill.info".
!
!  As a counterpart to the access to the source code and rights to copy, modify and redistribute granted by the
!  license, users are provided only with a limited warranty and the software's author, the holder of the economic
!  rights, and the successive licensors have only limited liability.
!
!  In this respect, the user's attention is drawn to the risks associated with loading, using, modifying and/or
!  developing or reproducing the software by the user in light of its specific status of free software, that may
!  mean that it is complicated to manipulate, and that also therefore means that it is reserved for developers and
!  experienced professionals having in-depth computer knowledge. Users are therefore encouraged to load and test the
!  software's suitability as regards their requirements in conditions enabling the security of their systems and/or
!  data to be ensured and, more generally, to use and operate it in the same conditions as regards security.
!
!  The fact that you are presently reading this means that you have had knowledge of the CeCILL license and that you
!  accept its terms.
!
!======================================================================================================================!
!> \file output.f90
!! \brief This file includes routines of creation of ouput file.

!**********************************************************************************************************************!
!**********************************************************************************************************************!
!
!  Generic routine to write a real in time with dtp time step (input.txt)
!
!**********************************************************************************************************************!
!**********************************************************************************************************************!

!>  Generic routine to write a real in time with dtp time step (input.txt)
!!
!! \details This subroutine write a real var in the file named 'file_name'.
!! \param[in]    var Real to print in the output file.
!! \param[in]    file_name Name of the file.
SUBROUTINE write_scalar_in_time( var , file_name )

   USE m_common


   implicit none

   !===================================================================================================================!
   !  Interface Variables
   !===================================================================================================================!

   real(rp), intent(in)  ::  var

   character(len=*), intent(in)  ::  file_name

   !===================================================================================================================!
   !  Local Variables
   !===================================================================================================================!

   character(len=lchar)  ::  var_name

   !===================================================================================================================!
   !  Begin Subroutine
   !===================================================================================================================!

   var_name  =  trim(file_name)

   call sub_write( 'gnuplot' ) 



   CONTAINS

      !>  Generic routine to write a real in time with dtp time step (input.txt)
      !!
      !! \details This subroutine writes a real var in the file named 'file_name' from the type of the file wanted.
      !! \param[in]    file_type Type of the file wanted ('gnuplot' only avaible).
      SUBROUTINE sub_write( file_type )

         !=============================================================================================================!
         !  Interface Variables
         !=============================================================================================================!

         character(len=*), intent(in)  ::  file_type

         !=============================================================================================================!
         !  Local Variables
         !=============================================================================================================!

         character(len=lchar)  ::  complete_file_name

         !=============================================================================================================!
         !  Begin Subroutine
         !=============================================================================================================!

         if (test_dt_nearest( dtp ) ) then

            complete_file_name = 'res/'//trim(file_name)//'.dat'

            if ( all( is_file_open(:) /= complete_file_name ) ) then

               inquire( file = complete_file_name , exist = file_exist(1) )

               if ( .not. file_exist(1) ) then

                  open(10,file=complete_file_name,status='replace',form='formatted')

                  select case( file_type )

                     case( 'tecplot' )

                        write(10,'(A,A,A)') 'VARIABLES = "time" "' , trim(var_name) , '"'

                     case default

                        write(10,'(A,A)') '# time ' , trim(var_name)

                  end select

                  close(10)

               end if

               file_open_counter  =  file_open_counter  +  1

               is_file_open( file_open_counter )  =  complete_file_name

            end if

            open(10,file=complete_file_name,status='old',position='append',form='formatted')

            write(10,'(2ES23.15)') tc , var

            close(10)

         end if

      END SUBROUTINE sub_write


END SUBROUTINE write_scalar_in_time


!**********************************************************************************************************************!
!**********************************************************************************************************************!
!
!  Generic routine to write n scalars with dtp time step (input.txt)
!
!**********************************************************************************************************************!
!**********************************************************************************************************************!

!>  Generic routine to write n scalars with dtp time step (input.txt)
!!
!! \details This subroutine writes n scalars real variable with name of the variable in the file named 'file_name'.
!! \param[in]    var Array of real to print in the file.
!! \param[in]    var_names Array of name variable to print in the file.
!! \param[inout]    file_name Name of the file.
!! \param[in]    nbvars Number of the variable to print.
SUBROUTINE write_pscalar( var , var_names , file_name , nbvars )

   USE m_common

   implicit none

   !===================================================================================================================!
   !  Interface Variables
   !===================================================================================================================!

   integer(ip), intent(in)  ::  nbvars

   real(rp)        , dimension(nbvars), intent(in)  ::  var
   character(len=*), dimension(nbvars), intent(in)  ::  var_names

   character(len=*), intent(inout)  ::  file_name

   !===================================================================================================================!
   !  Calling Generic sub_write with Output Options defined in 'input.txt'
   !===================================================================================================================!

   call sub_write( 'gnuplot' ) ; return


   CONTAINS

      !>  Generic routine to write a real in time with dtp time step (input.txt)
      !!
      !! \details This subroutine writes n scalars real variable with name of the variable in the file named 
      !! 'file_name'.
      !! \param[in]    file_type Type of the file wanted ('gnuplot' only avaible).
      SUBROUTINE sub_write( file_type )

         !=============================================================================================================!
         !  Interface Variables
         !=============================================================================================================!

         character(len=*), intent(in)  ::  file_type

         !=============================================================================================================!
         !  Local Variables
         !=============================================================================================================!

         character(len=lchar)  ::  complete_file_name

         integer(ip)  ::  ivar

         !=============================================================================================================!
         !  Begin Subroutine
         !=============================================================================================================!

         if (test_dt_nearest( dtp ) ) then

            complete_file_name = 'res/'//trim(file_name)//'.dat'

            if ( all( is_file_open(:) /= complete_file_name ) ) then

               inquire( file = complete_file_name , exist = file_exist(1) )

               if ( .not. file_exist(1) ) then

                  open(10,file=complete_file_name,status='replace',form='formatted')

                  write(10,'(A)',advance='no') '# '

                     do ivar = 1,nbvars

                        if ( ivar == nbvars ) then

                           write(10,'(A)'             ) trim( var_names( ivar ) )

                        else

                           write(10,'(A)',advance='no') trim( var_names( ivar ) ) // ' '

                        end if

                     end do

                  close(10)

               end if

               file_open_counter  =  file_open_counter  +  1

               is_file_open( file_open_counter )  =  complete_file_name

            end if

            open(10,file=complete_file_name,status='old',position='append',form='formatted')

            do ivar = 1,nbvars

               if ( ivar == nbvars ) then

                  write(10,'(ES23.15)'             ) var( ivar )

               else

                  write(10,'(ES23.15)',advance='no') var( ivar )

               end if

            end do

            close(10)

         end if

      END SUBROUTINE sub_write


END SUBROUTINE write_pscalar




